grammar org.xtext.example.cps.Cps with org.eclipse.xtext.common.Terminals

//generate cps "http://www.xtext.org/example/cps/Cps" //for steps 1 and 2
import "http://www.xtext.org/example/cps/Cps" // for steps 3, 4, 5

Cps:
    program=Program;

Program:
    'program' name=ID
    'courses' '{' courses+=Course* '}'
    'required' '{' requiredCourses+=[Course] (',' requiredCourses+=[Course])* '}'
    'students' '{' students+=Student* '}';

Student:
    'student' name=ID
    'taken' '{' taken+=Course* '}'
    'maxCreditsPerTerm' maxCredits=INT;

Course:
    'course' name=ID number=ID
    'credits' credits=INT
    'offered' offered=Term
    'year' year=INT
    ('prereq' prereq=Expr)?
    ('coreq' coreq=Expr)?;

enum Term:
    FALL='FALL' | WINTER='WINTER' | BOTH='BOTH';

Expr returns Expr:
      Or;

Or returns Expr:
    And ( {Or.left=current} 'OR' right=And )*;

And returns Expr:
    Atom ( {And.left=current} 'AND' right=Atom )*;

Atom returns Expr:
      CourseRef | '(' Expr ')';

CourseRef returns Expr:
    course=[Course];