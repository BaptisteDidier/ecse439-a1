grammar org.xtext.example.cps.Cps with org.eclipse.xtext.common.Terminals

//generate cps "http://www.xtext.org/example/cps/Cps" for steps 1 and 2
import "http://www.xtext.org/example/cps/Cps"

Cps:
    program=Program
    students+=Student*;

Program:
    'program' name=ID
    'courses' '{' courses+=Course* '}';

Student:
    'student' name=ID
    'taken' '{' takenCourses+=[Course]* '}'
    'maxCreditsPerTerm' maxCredits=INT;

Course:
    'course' name=ID number=ID
    'credits' credits=INT
    'term' term=Term
    'year' year=INT
    ('prereq' prereq=Expr)?
    ('coreq' coreq=Expr)?;

enum Term:
    FALL='FALL'
  | WINTER='WINTER'
  | BOTH='BOTH';

Expr:
    OrExpr;

OrExpr returns Expr:
    AndExpr ({Or.left=current} 'OR' right=AndExpr)*;

AndExpr returns Expr:
    Atom ({And.left=current} 'AND' right=Atom)*;

Atom returns Expr:
      course=[Course]
    | '(' Expr ')';